plugins {
    id 'com.android.application'
    id 'org.mozilla.rust-android-gradle.rust-android' version '0.9.3'
    id 'kotlin-android'
}

android {
    namespace 'com.cyberegylet.antiDupeGallery'
    compileSdk 34

    defaultConfig {
        applicationId "com.cyberegylet.antiDupeGallery"
        minSdk 26
        targetSdk 33
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.debug
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    sourceSets {
        main {
            jniLibs.srcDirs = ["src/main/jniLibs"]
        }
    }

    ndkVersion '26.1.10909125'
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_17.toString()
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion '1.5.9'
    }
}

cargo {
    module = '../backend'
    libname = 'backend'
    targets = ['arm64', "arm"]
    def isRelease = gradle.startParameter.taskRequests.any {
        it.args.any { it.endsWith("Release") }
    }
    if (isRelease) {
        profile = "release"
    }
}
preBuild.dependsOn 'cargoBuild'

def GLIDE_VERSION = "4.16.0"

dependencies {
    implementation 'androidx.activity:activity-ktx:1.8.2'
    implementation 'androidx.fragment:fragment-ktx:1.6.2'
    implementation "com.github.zjupure:webpdecoder:2.6.${GLIDE_VERSION}"
    implementation "com.github.bumptech.glide:glide:${GLIDE_VERSION}"
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.11.0'
    implementation 'com.davemorrissey.labs:subsampling-scale-image-view:3.10.0'
    implementation platform('androidx.compose:compose-bom:2024.02.00')
    implementation 'androidx.compose.material3:material3'
    implementation 'androidx.compose.ui:ui-tooling-preview'
    implementation 'androidx.activity:activity-compose'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-compose'
    implementation 'androidx.compose.ui:ui-tooling'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
}
